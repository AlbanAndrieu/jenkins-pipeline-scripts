    import com.test.gradle.*

plugins {
    id "io.codearte.nexus-staging" version "0.8.0"
}

apply plugin: 'groovy'
apply plugin: 'maven'
//apply plugin: 'signing'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    maven {
        url "https://maven.springframework.org/release"
    }
    maven {
        url "https://repo.jenkins-ci.org/public/"
    }
    maven {
      url 'http://repo.mycompany.com/maven2'
    }
}

configurations {
  ivy
}

group = "com.test"
archivesBaseName = "jenkins-pipeline-scripts"
version = "1.0-SNAPSHOT"

dependencies {
    compile    group: 'javax.servlet',         name: 'javax.servlet-api',     version: '3.1.0'
    compile    group: 'org.jenkins-ci.main',   name: 'jenkins-core',          version: '2.102'
    compile            group: 'org.codehaus.groovy',   name: 'groovy-all',            version: '2.4.6'
    compile            group: 'com.cloudbees',         name: 'groovy-cps',            version: '1.12'
    compile            group: 'commons-io',            name: 'commons-io',            version: '2.5'
    compile            group: 'org.assertj',           name: 'assertj-core',          version: '3.4.1'
    compile            group: 'org.apache.commons',    name: 'commons-math3',         version: '3.4.1'
    testCompile        group: 'com.lesfurets',         name: 'jenkins-pipeline-unit', version: '1.1'
    testCompile        group: 'org.assertj',           name: 'assertj-core',          version: '3.4.1'
    testCompile        group: 'junit',                 name: 'junit',                 version: '4.11'
    ivy                group: 'org.apache.ivy',        name: 'ivy',                   version: '2.4.0'
}

// Get external properties from file
if (project.hasProperty('extProps')) {
    def props = new Properties()
    file("$extProps").withInputStream { props.load(it) }
    props.stringPropertyNames().forEach {
        ext."$it" = props.getProperty(it)
    }
}

defaultTasks 'test'

tasks.withType(GroovyCompile) {
  groovyClasspath += configurations.ivy
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

//signing {
//    sign configurations.archives
//}

test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            def repo = project.hasProperty('repository') ?
                            project.repository :
                            "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            repository(url: repo) {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            def snapRepo = project.hasProperty('snapshotRepository') ?
                            project.snapshotRepository :
                            "https://oss.sonatype.org/content/repositories/snapshots/"
            snapshotRepository(url: snapRepo) {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "$project.group:$project.archivesBaseName"
                packaging 'jar'
                description 'Jenkins Pipeline Unit testing framework'
                url 'https://github.com/test/JenkinsPipelineUnit'

                scm {
                    url 'https://github.com/test/JenkinsPipelineUnit'
                    connection 'scm:git:git://github.com/test/JenkinsPipelineUnit.git'
                    developerConnection 'scm:git:git@github.com/test/JenkinsPipelineUnit.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'aandrieu'
                        name 'Albn Andrieu'
                        email 'alban.andrieu@free.fr'
                        url 'https://github.com/AlbanAndrieu'
                        organization 'sample.com'
                        organizationUrl 'https://github.com/sample'
                    }
                }
            }
        }
    }
}

nexusStaging {
    delayBetweenRetriesInMillis = 5000
}

// Deploy tasks
closeAndReleaseRepository.mustRunAfter(uploadArchives)
closeRepository.enabled = !project.version.toString().endsWith('-SNAPSHOT')
releaseRepository.enabled = !project.version.toString().endsWith('-SNAPSHOT')

task verify(dependsOn: build)
task deploy(dependsOn: [build, uploadArchives, closeAndReleaseRepository])

// Release commits and tag
//task writeVersion(type: SetVersionTask)
//task commitRelease(type: GitCommitTask, dependsOn: [writeVersion])
//task tagVersion(type: GitTagTask, dependsOn: [writeVersion, commitRelease])
//task writeNextSnapshot(type: SetNextSnapshotVersionTask)
//task commitNextDevVersion(type: GitCommitTask, dependsOn: [writeNextSnapshot]) {
//    message = "Next dev version"
//}

//task release(dependsOn: [writeVersion, tagVersion, commitRelease])
//task nextDevVersion(dependsOn: [writeNextSnapshot, commitNextDevVersion])
